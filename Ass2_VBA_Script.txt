Sub Ticker_Testing2()

  ' Set an initial variable for holding the brand name
  Dim Ticker_Name As String

  ' Set an initial variable for holding the total per ticker
  Dim Ticker_Difference As Double
  Dim Ticker_Close As Double
  Dim Ticker_Open As Double
  Dim Ticker_Percentage As Double
  Dim Stock_Volume As Double
  Dim j As Double
  Dim ws As Worksheet
  Dim maxValue As Double
  Dim minValue As Double
  Dim GreatestTotalVolumes As Double
  
  ' Loop through each worksheet in the workbook
  For Each ws In ThisWorkbook.Worksheets
    Ticker_Difference = 0
    Ticker_Close = 0
    Ticker_Percentage = 0
    Stock_Volume = 0
    j = 0
    maxValue = 0
    minValue = 0
    GreatestTotalVolumes = 0
    
    ' Add headers for each value
    ws.Range("I" & 1).Value = "Ticker"
    ws.Range("J" & 1).Value = "Yearly Change"
    ws.Range("K" & 1).Value = "Percent Change"
    ws.Range("L" & 1).Value = "Total Stock Volume"
    
    'Change column size to fit new headers
    
    ws.Columns("J").AutoFit
    ws.Columns("K").AutoFit
    ws.Columns("L").AutoFit
    
    ' Keep track of the location for each ticker in the summary table of tickers
    Dim Summary_Table_Row As Integer
    Summary_Table_Row = 2

    ' Determine the Last Row for the current worksheet
    LastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row

    ' Loop through all tickers in the current worksheet
    For I = 2 To LastRow
      ' Check if we are comparing the same ticker, if it is not...
      If ws.Cells(I + 1, 1).Value <> ws.Cells(I, 1).Value Then
        ' Set the new Ticker name
        Ticker_Name = ws.Cells(I, 1).Value

        ' Plug in values for the Ticker Difference which is opening price at the
        'beginning of a given year to the closing price at the end of that year.
        'j value represents the total rows - 1 from that specific ticker
        Ticker_Difference = ws.Cells(I, 6).Value - ws.Cells(I - j, 3).Value

        ' Create Ticker percentage
        Ticker_Percentage = (ws.Cells(I, 6).Value - ws.Cells(I - j, 3).Value) / ws.Cells(I - j, 3)
        ws.Range("K" & Summary_Table_Row).NumberFormat = "0.00%"

        ' Add to the Stock Volume
        Stock_Volume = Stock_Volume + ws.Cells(I, 7).Value

        ' Print the Ticker in the Summary Table of Tickers
        ws.Range("I" & Summary_Table_Row).Value = Ticker_Name

        ' Print the Ticker Difference in the Summary Table
        ws.Range("J" & Summary_Table_Row).Value = Ticker_Difference

        ' Color the percentage green if positive or red if negative value
        If (ws.Range("J" & Summary_Table_Row).Value > 0) Then
          ws.Range("J" & Summary_Table_Row).Interior.ColorIndex = 4
        Else
          ws.Range("J" & Summary_Table_Row).Interior.ColorIndex = 3
        End If

        ' Print the percentage in the Summary Table
        ws.Range("K" & Summary_Table_Row).Value = Ticker_Percentage

        ' Print the Stock Volume in the Summary Table
        ws.Range("L" & Summary_Table_Row).Value = Stock_Volume

        ' Move to the next row in the Summary Table
        Summary_Table_Row = Summary_Table_Row + 1

        ' Reset variables for the next ticker
        Stock_Volume = 0
        Ticker_Difference = 0
        j = 0
      Else
        ' Add to the Stock Volume for the same Ticker
        Stock_Volume = Stock_Volume + ws.Cells(I, 7).Value
        j = j + 1
      End If
    Next I
    
    'Add names for each value
    
    ws.Range("O" & 2).Value = "Greatest % Increase"
    ws.Range("O" & 3).Value = "Greatest % Decrease"
    ws.Range("O" & 4).Value = "Greatest Total Volume"
    
    'Change O column size to fit new names
    
    ws.Columns("O").AutoFit
    
    ' Loop through all rows in the Summary Table of Tickers to find tickers with greatest % increase, greatest % decrease,
    'and greatest total volume of stocks respectively
    For k = 2 To Summary_Table_Row
      If ws.Cells(k, 11).Value > maxValue Then
          maxValue = ws.Cells(k, 11).Value
          ws.Range("Q" & 2).NumberFormat = "0.00%"
          ws.Range("Q" & 2).Value = maxValue
          ws.Range("P" & 2).Value = ws.Cells(k, 9).Value
      End If
      If ws.Cells(k, 11).Value < minValue Then
          minValue = ws.Cells(k, 11).Value
          ws.Range("Q" & 3).NumberFormat = "0.00%"
          ws.Range("Q" & 3).Value = minValue
          ws.Range("P" & 3).Value = ws.Cells(k, 9).Value
      End If
      If ws.Cells(k, 12).Value > GreatestTotalVolumes Then
          GreatestTotalVolumes = ws.Cells(k, 12).Value
          ws.Range("Q" & 4).Value = GreatestTotalVolumes
          ws.Range("P" & 4).Value = ws.Cells(k, 9).Value
      End If
    Next k
  Next ws
End Sub

